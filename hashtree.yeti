module hashtree;

typedef hashtree = hash<'a, Sub 'b | Ends ()>;

empty () = [:];

addList tree lst is hashtree -> list<'a> -> boolean = 
    case lst of
    node::rest:
        if not node in tree then 
            if empty? rest then
                tree[node] := Ends ();
                true;
            else
                sub = [:];
                tree[node] := Sub sub;
                addList sub rest;
            fi;
        else
            case tree[node] of 
                Sub sub: addList sub rest;
                Ends (): empty? rest;
            esac;
        fi;
    _:  false; 
    esac;

exactMatch tree lst is hashtree -> list<'a> -> boolean =
    case lst of
    node::rest:
        if not node in tree then false
        else
            case tree[node] of
                Sub sub: exactMatch sub rest;
                Ends (): empty? rest;
            esac
        fi;
    _: false;
    esac;

enumerate tree is hashtree -> list<'a> =
    concat
       (map do k:
            case tree[k] of
            Sub sub: map do t: k::t done (enumerate sub);
                  _: [[k]];
            esac
        done (keys tree));

{ empty, addList = addList, exactMatch, enumerate }

