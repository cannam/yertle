/*
    Yertle
    RDF/Turtle example code

    Copyright (c) 2013 Chris Cannam
    Copyright (c) 2013 Queen Mary, University of London
  
    Permission is hereby granted, free of charge, to any person
    obtaining a copy of this software and associated documentation
    files (the "Software"), to deal in the Software without
    restriction, including without limitation the rights to use, copy,
    modify, merge, publish, distribute, sublicense, and/or sell copies
    of the Software, and to permit persons to whom the Software is
    furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be
    included in all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
    EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
    MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
    NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR
    ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
    CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
    WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

    Except as contained in this notice, the names of the Centre for
    Digital Music; Queen Mary, University of London; and Chris Cannam
    shall not be used in advertising or otherwise to promote the sale,
    use or other dealings in this Software without prior written
    authorization.
*/

program example.count;

// Parse a Turtle (or N-Triples) file and count the triples in it.

read = load yertle.read;

usage () =
   (eprintln "\nUsage: count infile.ttl";
    eprintln "\n   Counts the triples in infile.ttl (Turtle or N-Triples format)\n");

time description something =
   (before = System#currentTimeMillis();
    result = something ();
    after = System#currentTimeMillis();
    println "Time for \(description): \(after - before) ms";
    result);

countTriples n tt =
    case tt of
    first::rest:
        case first of
        Prefix pfx: countTriples n rest;
        Triple t: countTriples (n+1) rest;
        Error e: println "Error: \(e)"; -1;
        esac;
    _: n
    esac;

process infile =
   (n = time "count" \(countTriples 0 (read.parseTurtleFile "" infile));
    if n >= 0 then
        println "Counted \(n) triple(s)"
    else
        println "Could not parse file"
    fi);

case (list _argv) of
infile::[]: process infile;
_: usage ();
esac;

