/*
    Yertle
    RDF/Turtle prefix expand and abbreviate

    Copyright (c) 2013-2014 Chris Cannam
    Copyright (c) 2013-2014 Queen Mary, University of London
  
    Permission is hereby granted, free of charge, to any person
    obtaining a copy of this software and associated documentation
    files (the "Software"), to deal in the Software without
    restriction, including without limitation the rights to use, copy,
    modify, merge, publish, distribute, sublicense, and/or sell copies
    of the Software, and to permit persons to whom the Software is
    furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be
    included in all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
    EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
    MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
    NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR
    ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
    CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
    WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

    Except as contained in this notice, the names of the Centre for
    Digital Music; Queen Mary, University of London; and Chris Cannam
    shall not be used in advertising or otherwise to promote the sale,
    use or other dealings in this Software without prior written
    authorization.
*/

module yertle.prefix;

load yertle.types;

typedef ptable = {
    forward is hash<string, string>,
    inverse is hash<string, string>
};

typedef opaque prefixtable = ptable;

newPrefixTable () is () -> ptable = {
    forward = [:],
    inverse = [:]
};

addPrefix ptab pfx iri =
   (ptab.forward[pfx] := iri;
    ptab.inverse[iri] := pfx);

havePrefix ptab pfx = pfx in ptab.forward;

expand ptab pname =
   (n = strIndexOf pname ":" 0;
    if n < 0 then pname
    else
        pfx = strLeft pname n;
        if pfx in ptab.forward then ptab.forward[pfx] ^ (strRight pname (n+1))
        else pname
        fi
    fi);

abbreviate ptab fullname =
   (pfxFor name =
        if name in ptab.inverse or name == "" then name
        else pfxFor (strLeft name ((strLength name) - 1))
        fi;
    case pfxFor fullname of
      "": fullname;
     pfx: ptab.inverse[pfx] ^ ":" ^ (strRight fullname (strLength pfx))
    esac);

prefixes ptab = keys ptab.forward;

{
newPrefixTable,
addPrefix,
havePrefix,
expand,
abbreviate,
prefixes
} as {
newPrefixTable is () -> prefixtable,
addPrefix is prefixtable -> string -> string -> (),
havePrefix is prefixtable -> string -> boolean,
expand is prefixtable -> string -> string,
abbreviate is prefixtable -> string -> string,
prefixes is prefixtable -> list<string>
}






