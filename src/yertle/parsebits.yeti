/*
    Yertle
    RDF/Turtle parser module

    Copyright (c) 2013 Chris Cannam
    Copyright (c) 2013 Queen Mary, University of London
  
    Permission is hereby granted, free of charge, to any person
    obtaining a copy of this software and associated documentation
    files (the "Software"), to deal in the Software without
    restriction, including without limitation the rights to use, copy,
    modify, merge, publish, distribute, sublicense, and/or sell copies
    of the Software, and to permit persons to whom the Software is
    furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be
    included in all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
    EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
    MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
    NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR
    ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
    CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
    WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

    Except as contained in this notice, the names of the Centre for
    Digital Music; Queen Mary, University of London; and Chris Cannam
    shall not be used in advertising or otherwise to promote the sale,
    use or other dealings in this Software without prior written
    authorization.
*/

module yertle.parsebits;

whitespace state =
   (r = state.reader;
    r.isWhitespace and not r.isEof loop r.discard ();
    if r.next == "#" then
        not r.isEol and not r.isEof loop r.discard ();
        whitespace state
    else
        OK (Empty ())
    fi);

chomp state = // read whitespace, discard, and return nothing
   (case whitespace state of OK _: (); esac);

required expected state =
    if state.reader.next == expected then
       state.reader.discard (); OK (Empty ());
    else
       Error "Expected '\(expected)', found '\(state.reader.next)' at \(state.reader.location)";
    fi;

// Given result a and function b, if a is an Error, return that
// error; if a is an OK, pass its value to b and return that
(~>) a b =
    case a of
    OK result: b result;
    Error e: Error e;
    esac;

noseq state seq =
    case seq of
    f::rest: f state ~> \(noseq state rest);
    _: OK (Empty ());
    esac;

tokseq state seq =
   (tokseq' acc state seq =
        case seq of
        f::rest:
            f state ~>
           \case of
            Token t: tokseq' (OK (Token t)) state rest;
                  _: tokseq' acc state rest;
            esac;
        _: acc;
        esac;
    case tokseq' (Error "") state seq of
    Error _: Error "No token found at \(state.reader.location)";
    other: other;
    esac);

{ whitespace, chomp, required, ~>, noseq, tokseq }

