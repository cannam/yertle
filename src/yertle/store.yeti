/*
    Yertle
    Simple RDF store

    Copyright (c) 2013 Chris Cannam
    Copyright (c) 2013 Queen Mary, University of London
  
    Permission is hereby granted, free of charge, to any person
    obtaining a copy of this software and associated documentation
    files (the "Software"), to deal in the Software without
    restriction, including without limitation the rights to use, copy,
    modify, merge, publish, distribute, sublicense, and/or sell copies
    of the Software, and to permit persons to whom the Software is
    furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be
    included in all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
    EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
    MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
    NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR
    ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
    CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
    WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

    Except as contained in this notice, the names of the Centre for
    Digital Music; Queen Mary, University of London; and Chris Cannam
    shall not be used in advertising or otherwise to promote the sale,
    use or other dealings in this Software without prior written
    authorization.
*/

module yertle.store;

index = load yertle.index;

prefix = load yertle.prefix;
rdftypes = load yertle.rdftypes;

load yertle.types;

typedef tripleselector = triple -> node;

typedef patternselector = pattern -> patnode;

typedef indextree = hash<node,
                         Sub hash<node,
                                  Sub hash<node,
                                           Entry array<node>>>>;

typedef index = {
    .order is list<tripleselector>,
    .patternOrder is list<patternselector>,
    .tree is indextree
};

typedef storetype = {
    .prefixes is prefixtable,
    .indexes is list<index>,
};

newStore () = 
   (desired = [ "spo", "pos", "ops" ];
    orderMap = [ "s": (.s), "p": (.p), "o": (.o) ];
    patternOrderMap = [ "s": (.s), "p": (.p), "o": (.o) ];
    prefixes = prefix.newPrefixTable ();
    prefix.addPrefix prefixes "rdf" rdftypes.rdfPrefix;
    prefix.addPrefix prefixes "xsd" rdftypes.xsdPrefix;
    {
        prefixes,
        indexes =
            map do name:
                index.newIndex
                    (map (at orderMap) (tail (strSplit "" name)))
                    (map (at patternOrderMap) (tail (strSplit "" name)))
                done
                desired
    }
    );

add st t =
    if all id (map do ix: index.add ix t done st.indexes) then ()
    else throw new Exception("Internal error: add failed for \(t) in \(st)");
    fi;

remove st t =
    all id (map do ix: index.remove ix t done st.indexes);

enumerate st =
    index.enumerate (head st.indexes);

contains st t =
    // Doesn't matter which index we use to look up a complete triple
    index.contains (head st.indexes) t;

match st pat =
    index.match (index.chooseIndex st.indexes pat) pat;

expand' st pname = 
    if pname == "a" then 
        expand' st "rdf:type"
    else
        IRI (prefix.expand st.prefixes pname);
    fi;

abbreviate' st iri =
    case iri of
    IRI i:
        case prefix.abbreviate st.prefixes i of
        "rdf:type": "a";
             other: other
        esac
    esac;

{
/**
 * Create a new indexed RDF triple store object.
 */
newRdfStore () =
   (s = newStore ();
    sync = synchronized s;
    { 
        norec add t = sync \(add s t),
        norec remove t = sync \(remove s t),
        norec enumerate () = sync \(enumerate s),
        norec contains t = sync \(contains s t),
        norec match pat = sync \(match s pat),
        addPrefix pfx exp = sync \(prefix.addPrefix s.prefixes pfx exp),
        havePrefix pfx = sync \(prefix.havePrefix s.prefixes pfx),
        prefixes () = sync \(prefix.prefixes s.prefixes),
        expand str = sync \(expand' s str),
        abbreviate str = sync \(abbreviate' s str),
    } as {

        /**
         * Add a triple to the store.
         *
         * Example: 
         * store.add { s = IRI "http://example.org/person/fred", p = store.expand "foaf:name", o = Literal { value = "Fred", type = "", language = "" } }
         */
        add is triple -> (),
        
        /**
         * Remove a triple from the store. Return true if the triple
         * was successfully removed, false if not found in the store.
         */
        remove is triple -> boolean,

        /**
         * Return a list of all triples in the store (in arbitrary
         * order).
         */
        enumerate is () -> list<triple>,

        /**
         * Test whether a triple is in the store.
         */
        contains is triple -> boolean,

        /**
         * Given a pattern, return all triples in the store that
         * match the pattern. Patterns may have Known or Wildcard 
         *
         * Example:
         * store.match { s = Wildcard (), p = Known (store.expand "foaf:name"), o = Wildcard () }
         */
        match is pattern -> list<triple>,

        /**
         * Add a prefix for future use by the expand and abbreviate
         * functions.
         *
         * Example:
         * store.addPrefix "foaf" "http://xmlns.com/foaf/0.1/"
         */
        addPrefix is string -> string -> (),

        /**
         * Enquire whether the store knows about the given prefix.
         */
        havePrefix is string -> boolean,

        /**
         * Return all the prefixes (prefix parts only) in the store.
         */
        prefixes is () -> list<string>,

        /**
         * Expand a prefixed name into an IRI.
         *
         * Example:
         * store.expand "foaf:name"
         * 
         * which will return (IRI "http://xmlns.com/foaf/0.1/name")
         * if the foaf prefix has been added to this store (see
         * addPrefix). If the prefixed name has an unknown prefix, 
         * it will be returned unexpanded.
         */
        expand is string -> iri,

        /**
         * Abbreviate an IRI into a prefixed name.
         *
         * Example:
         * store.abbreviate (IRI "http://xmlns.com/foaf/0.1/name")
         *
         * which will return "foaf:name" if the foaf prefix has
         * been added to this store (see addPrefix).
         *
         * If no known prefix matches the given IRI, the IRI will
         * be returned unchanged (but as a plain string rather 
         * than a tagged type).
         */
        abbreviate is iri -> string
    });
}

