/*
    Yertle
    RDF/Turtle parser module

    Copyright (c) 2013 Chris Cannam
    Copyright (c) 2013 Queen Mary, University of London
  
    Permission is hereby granted, free of charge, to any person
    obtaining a copy of this software and associated documentation
    files (the "Software"), to deal in the Software without
    restriction, including without limitation the rights to use, copy,
    modify, merge, publish, distribute, sublicense, and/or sell copies
    of the Software, and to permit persons to whom the Software is
    furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be
    included in all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
    EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
    MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
    NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR
    ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
    CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
    WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

    Except as contained in this notice, the names of the Centre for
    Digital Music; Queen Mary, University of London; and Chris Cannam
    shall not be used in advertising or otherwise to promote the sale,
    use or other dealings in this Software without prior written
    authorization.
*/

module yertle.char;

/**
 * Read lines from handle and make them available through a char-by-char
 * interface with optional regex consumption.
 */
reader handle =
   (var line = "";
    var lineno = 0;
    var colno = 0;

    ws c = (c == " " or c == "\n" or c == "\t" or c == "\r");
    eol c = (c == "\n" or c == "\r");

    loadLine () = (line := handle.readln (); colno := 0; lineno := lineno + 1);

    charAt n =
        if n >= strLength line then "\n"
        else strChar line n
        fi;

    advance () =
       (colno := colno + 1;
        if colno > strLength line then loadLine () fi);

    lookingAt likeMatcher =
        not empty? (likeMatcher (strRight line colno) ());

    match likeMatcher =
        case likeMatcher (strRight line colno) () of
        [match]: match;
        _: "";
        esac;

    readMatch likeMatcher =
       (m = match likeMatcher;
        colno := colno + strLength m;
        if colno > strLength line then loadLine () fi;
        m);

    loadLine ();

    {
        get next () = charAt colno,
        read () = (c = charAt colno; advance (); c),
        discard () = advance (),
        lookingAt,
        match,
        readMatch,
        get isWhitespace () = ws (charAt colno),
        get isEol () = eol (charAt colno),
        get isEof () = not defined? line,
        get location () = "line \(lineno), column \(colno)",
        close () = handle.close();
    }
    );

{ reader }
