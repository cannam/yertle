
module yertle.index;

typedef indextree = hash<'a, Sub 'b | Entry 'c>;

typedef index = { order is 'a, tree is indextree };

newIndex order = { order, tree = [:] };

add ix stmt is index -> 'a -> boolean =
    case ix.order of
    selector::rest:
        node = selector stmt;
        if not node in ix.tree then 
            if empty? rest then 
                ix.tree[node] := Entry stmt;
                true;
            else
                sub = [:];
                ix.tree[node] := Sub sub;
                add { order = rest, tree = sub } stmt;
            fi;
        else
            case ix.tree[node] of
                Sub sub: add { order = rest, tree = sub } stmt;
                Entry s: empty? rest and s == stmt;
            esac;
        fi;
    _: false;
    esac;

remove ix stmt is index -> 'a -> boolean =
    case ix.order of
    selector::rest:
        node = selector stmt;
        if not node in ix.tree then false 
        else
            case ix.tree[node] of
                Sub sub: remove { order = rest, tree = sub } stmt;
                Entry s: empty? rest and (delete ix.tree node; true);
            esac;
        fi;
    _: false;
    esac;

enumerate' tree is 'a -> list<'b> =
    concat
       (map do k:
            case tree[k] of
            Sub sub: enumerate' sub;
            Entry e: [e];
            esac
        done (keys tree));

enumerate ix is index -> list<'a> =
    enumerate' ix.tree;

match ix pat is index -> 'a -> list<'b> =
    concat
       (case ix.order of
        selector::rest:
            node = selector pat;
            matching = case node of
                       Wildcard _: keys ix.tree;
                       _: if node in ix.tree then [node] else [] fi;
                       esac;
            map do m:
                case ix.tree[m] of
                Sub sub: match { order = rest, tree = sub } pat;
                Entry e: [e];
                esac
            done matching;
        _: [];
        esac);

contains ix stmt is index -> 'a -> boolean =
    not empty? (match ix stmt);

scoreIndexFor pat order is 'a -> 'b -> number =
    case order of
    selector::rest:
        if (selector pat) == Wildcard () then 0 else 16 fi
            + (scoreIndexFor pat rest) / 2;
    _: 0;
    esac;

chooseIndex ixs pat is list<index> -> 'a -> index =
   (h = mapIntoHash ((scoreIndexFor pat) . (.order)) id ixs;
    best = head (sortBy (>) (keys h));
    h[best]);

{ newIndex, add, remove, enumerate, contains, match, chooseIndex }


