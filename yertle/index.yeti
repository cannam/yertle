/*
    Yertle
    Index for statements in RDF store

    Copyright (c) 2013 Chris Cannam
    Copyright (c) 2013 Queen Mary, University of London
  
    Permission is hereby granted, free of charge, to any person
    obtaining a copy of this software and associated documentation
    files (the "Software"), to deal in the Software without
    restriction, including without limitation the rights to use, copy,
    modify, merge, publish, distribute, sublicense, and/or sell copies
    of the Software, and to permit persons to whom the Software is
    furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be
    included in all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
    EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
    MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
    NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR
    ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
    CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
    WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

    Except as contained in this notice, the names of the Centre for
    Digital Music; Queen Mary, University of London; and Chris Cannam
    shall not be used in advertising or otherwise to promote the sale,
    use or other dealings in this Software without prior written
    authorization.
*/

module yertle.index;

typedef statement = 'a;

typedef pattern = 'a;

typedef indextree = hash<'a, Sub 'b | Entry 'c>;

typedef index = { order is list<'a>, patternOrder is list<'b>, tree is indextree };

newIndex order patternOrder = { order, patternOrder, tree = [:] };

add ix stmt is index -> statement -> boolean =
    case ix.order of
    selector::rest:
        node = selector stmt;
        if not node in ix.tree then 
            if empty? rest then 
                ix.tree[node] := Entry stmt;
                true;
            else
                sub = [:];
                ix.tree[node] := Sub sub;
                add { order = rest, tree = sub } stmt;
            fi;
        else
            case ix.tree[node] of
                Sub sub: add { order = rest, tree = sub } stmt;
                Entry s: empty? rest and s == stmt;
            esac;
        fi;
    _: false;
    esac;

remove ix stmt is index -> statement -> boolean =
    case ix.order of
    selector::rest:
        node = selector stmt;
        if not node in ix.tree then false 
        else
            case ix.tree[node] of
                Sub sub: remove { order = rest, tree = sub } stmt;
                Entry s: empty? rest and (delete ix.tree node; true);
            esac;
        fi;
    _: false;
    esac;

enumerate' tree is 'a -> list<statement> =
    concat
       (map do k:
            case tree[k] of
            Sub sub: enumerate' sub;
            Entry e: [e];
            esac
        done (keys tree));

enumerate ix is index -> list<statement> =
    enumerate' ix.tree;

match ix pat is index -> pattern -> list<statement> =
    concat
       (case ix.patternOrder of
        selector::rest:
           (matching = case selector pat of
                       Wildcard _: keys ix.tree;
                       Known node: if node in ix.tree then [node] else [] fi;
                       esac;
            map do m:
                case ix.tree[m] of
                Sub sub: match { patternOrder = rest, tree = sub } pat;
                Entry e: [e];
                esac
            done matching);
        _: [];
        esac);

contains ix stmt is index -> statement -> boolean =
   (case ix.order of
    selector::rest:
       (node = selector stmt;
        node in ix.tree and
            case ix.tree[node] of
            Sub sub: contains { order = rest, tree = sub } stmt;
            Entry e: true;
            esac);
    _: false;
    esac);

scoreIndexFor pat patternOrder is pattern -> 'b -> number =
    case patternOrder of
    selector::rest:
        case selector pat of
        Wildcard _: 0;
        Known _: 16;
        esac + (scoreIndexFor pat rest) / 2;
    _: 0;
    esac;

chooseIndex ixs pat is list<index> -> pattern -> index =
   (h = mapIntoHash ((scoreIndexFor pat) . (.patternOrder)) id ixs;
    best = head (sortBy (>) (keys h));
    h[best]);

{ newIndex, add, remove, enumerate, contains, match, chooseIndex }


