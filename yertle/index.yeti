
module yertle.index;

typedef literal = { value is string, type is string };

typedef statement = {
    s is IRI string | Blank number,
    p is IRI string,
    o is IRI string | Blank number | Literal literal,
};

typedef pattern = {
    s is Wildcard () | IRI string | Blank number,
    p is Wildcard () | IRI string,
    o is Wildcard () | IRI string | Blank number | Literal literal,
};

typedef indextree = hash<IRI string | Blank number | Literal literal, Sub 'b | Entry 'c>;

typedef index = { order is list<'a>, tree is indextree };

newIndex order = { order, tree = [:] };

add ix stmt is index -> statement -> boolean =
    case ix.order of
    selector::rest:
        node = selector stmt;
        if not node in ix.tree then 
            if empty? rest then 
                ix.tree[node] := Entry stmt;
                true;
            else
                sub = [:];
                ix.tree[node] := Sub sub;
                add { order = rest, tree = sub } stmt;
            fi;
        else
            case ix.tree[node] of
                Sub sub: add { order = rest, tree = sub } stmt;
                Entry s: empty? rest and s == stmt;
            esac;
        fi;
    _: false;
    esac;

remove ix stmt is index -> statement -> boolean =
    case ix.order of
    selector::rest:
        node = selector stmt;
        if not node in ix.tree then false 
        else
            case ix.tree[node] of
                Sub sub: remove { order = rest, tree = sub } stmt;
                Entry s: empty? rest and (delete ix.tree node; true);
            esac;
        fi;
    _: false;
    esac;

//!!! how to make this lazy?
enumerate' tree is 'a -> list<statement> =
    concat
       (map do k:
            case tree[k] of
            Sub sub: enumerate' sub;
            Entry e: [e];
            esac
        done (keys tree));

enumerate ix is index -> list<statement> =
    enumerate' ix.tree;

match ix pat is index -> pattern -> list<statement> =
    concat
       (case ix.order of
        selector::rest:
           (node = selector pat;
            matching = case node of
                       Wildcard _: keys ix.tree;
                       _: if node in ix.tree then [node] else [] fi;
                       esac;
            map do m:
                case ix.tree[m] of
                Sub sub: match { order = rest, tree = sub } pat;
                Entry e: [e];
                esac
            done matching);
        _: [];
        esac);

contains ix stmt is index -> statement -> boolean =
    not empty? (match ix stmt);

scoreIndexFor pat order is pattern -> 'b -> number =
    case order of
    selector::rest:
        case selector pat of
        Wildcard _: 0;
                 _: 16
        esac + (scoreIndexFor pat rest) / 2;
    _: 0;
    esac;

chooseIndex ixs pat is list<index> -> pattern -> index =
   (h = mapIntoHash ((scoreIndexFor pat) . (.order)) id ixs;
    best = head (sortBy (>) (keys h));
    h[best]);

{ newIndex, add, remove, enumerate, contains, match, chooseIndex }


