/*
    Yertle
    RDF/Turtle serialiser module

    Copyright (c) 2013 Chris Cannam
    Copyright (c) 2013 Queen Mary, University of London
  
    Permission is hereby granted, free of charge, to any person
    obtaining a copy of this software and associated documentation
    files (the "Software"), to deal in the Software without
    restriction, including without limitation the rights to use, copy,
    modify, merge, publish, distribute, sublicense, and/or sell copies
    of the Software, and to permit persons to whom the Software is
    furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be
    included in all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
    EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
    MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
    NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR
    ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
    CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
    WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

    Except as contained in this notice, the names of the Centre for
    Digital Music; Queen Mary, University of London; and Chris Cannam
    shall not be used in advertising or otherwise to promote the sale,
    use or other dealings in this Software without prior written
    authorization.
*/

module yertle.write;

load yertle.rdftypes;
load yertle.ttlre;

{ isCollectionNode, triplesOfCollection } = load yertle.collection;

import java.lang: Character;

// ASCII characters valid in strings: cautiously \u-encode in strings
asciiMatcher = matchAll "[^\\x20-\\x21\\x23-\\x5b\\x5d-\\x7e]" Escape Good;

// pname local part: cautiously %-encode in pnames
localMatcher = matchAll "[^\(rangePNBase)_:0-9]" Escape Good;

// IRIs: percent or u-escape all but non-reserved ASCII
iriMatcher = matchAll "[^A-Za-z0-9!#$&'()*+,./:;=?@\\[\\]_~-]" Escape Good;

applyEncoder encoder matcher text =
    strJoin ""
       (map
        \case of
         Good t: t;
         Escape [c]: encoder c;
         _: ""
         esac (matcher text));

percentPrefix hexString = 
    "%" ^ if (strLength hexString < 2) then "0" ^ hexString else hexString fi;

uPrefix hexString =
   (u = if (strLength hexString) < 5 then "u" else "U" fi;
    n = if (strLength hexString) < 5 then 4 else 8 fi;
    "\\\(u)" ^ (strPad "0" (n - strLength hexString) "") ^ hexString);

hexStringFor c = 
    strUpper Integer#toHexString(Character#codePointAt(c is string, 0));

asciiEncode text =
   (encode c = 
        case c of 
        "\t": "\\t";
        "\n": "\\n";
        "\r": "\\r";
        "\"": "\\\"";
        "\\": "\\\\";
           _: uPrefix (hexStringFor c);
        esac;
    applyEncoder encode asciiMatcher text);

percentEncode matcher text =
    // encoding chars that are ASCII but not legal unescaped in IRIs
    // in Turtle, and chars that are not ASCII but are legal code
    // points below 0xff
   (encode c = percentPrefix (hexStringFor c);
    applyEncoder encode matcher text);

iriEncode text = 
   (encode c = 
       (s = hexStringFor c;
        if (strLength s) < 3 then percentPrefix s
        else uPrefix s
        fi);
    applyEncoder encode iriMatcher text);

localEncode str = 
   (ix = strIndexOf str ":" 0;
    if ix >= 0 then
        // We can't do percent encoding and then escape encoding (for
        // the Turtle PLX rule) because that would give us \%NN where
        // we need %NN. But we also can't do escape encoding and then
        // percent encoding, because that would give us %5Cx where we
        // need \x. Easiest thing is just to percent encode the
        // escapable chars as well (which also gives compatibility
        // with earlier Turtle specs).
        (strLeft str (ix+1)) ^ (percentEncode localMatcher (strRight str (ix+1)))
    else
        str
    fi);

writeContextFreeNode handle node =
    case node of
    IRI u:
        handle.write "<\(iriEncode u)>";
    Blank b:
        handle.write "_:blank\(b)";
    Literal l:
       (handle.write "\"\(asciiEncode l.value)\"";
        if l.language != "" then
            handle.write "@\(l.language)";
        elif l.type != "" then
            handle.write "^^";
            writeContextFreeNode handle (IRI l.type);
        fi)
    esac;

writeNTriple handle triple = // no state needed for NTriples
    for [ (.s), (.p), (.o) ] do sel:
        writeContextFreeNode handle (sel triple);
        handle.write " ";
    done;

writeAbbreviatedNode state node =
    case node of
    IRI u:
        abbr = state.store.abbreviate node;
        if abbr == u then
            state.handle.write "<\(iriEncode u)>";
        else
            state.handle.write "\(localEncode abbr)";
        fi;
    Blank b:
        state.handle.write "_:blank\(b)";
    Literal l:
        if l.language != "" then
            state.handle.write "\"\(asciiEncode l.value)\"@\(l.language)";
        elif l.type != "" then
            state.handle.write "\"\(asciiEncode l.value)\"^^";
            writeAbbreviatedNode state (IRI l.type);
        else
            state.handle.write "\"\(asciiEncode l.value)\"";
        fi;
    esac;

var writeTurtleTriples' state triples = ();

writeAnonObject state triple =
   (state.handle.write "[ ";
    anonTriples = state.store.match 
        { s = Known triple.o, p = Wildcard (), o = Wildcard () };
    writeTurtleTriples' 
        (state with { subject = Some triple.o, predicate = None () })
        anonTriples;
    state.handle.write " ]");

writeTurtleObject state triple =
    if state.isAnon then
        state.handle.write " ";
        writeAnonObject state triple
    else
        state.handle.write " ";
        writeAbbreviatedNode state triple.o;
    fi;

writeTurtleNodes state selectors triple =
   (state.handle.write (strPad " " state.indent "");
    var first = true;
    for selectors do sel:
        if first then first := false else state.handle.write " " fi;
        writeAbbreviatedNode state (sel triple);
    done);

writeTurtleCollection state triple =
    // it's the object of this triple that is the collection node
   (triples = triplesOfCollection state.store triple.o;
    state.handle.write " (";
    for triples do triple:
        if triple.p == rdfFirstIRI then
            state.handle.write " ";
            writeAbbreviatedNode state triple.o;
        fi;
        state.written[triple] := true;
    done;
    state.handle.write " )");

writeTripleParts state triple =
   (case state.subject of
    None ():
        // first triple in graph
        writeTurtleNodes state [ (.s), (.p) ] triple;
    Some sn:
        if sn == triple.s then
            case state.predicate of
            None (): 
                // first triple in bnode [] syntax
                writeTurtleNodes (state with { indent = 0 }) [ (.p) ] triple;
            Some pn:
                if pn == triple.p then
                    state.handle.write ",";
                else
                    state.handle.write " ;\n";
                    writeTurtleNodes (state with { indent = 4 }) [ (.p) ] triple;
                fi;
            esac;
        else
            state.handle.write " .\n\n";
            writeTurtleNodes state [ (.s), (.p) ] triple;
        fi
    esac;
    if state.isCollection then
        writeTurtleCollection state triple;
    else
        writeTurtleObject state triple;
    fi;
    state with { subject = Some triple.s, predicate = Some triple.p });

hasBlankSubject t = case t.s of Blank _: true; IRI _: false esac;
hasBlankObject t = case t.o of Blank _: true; IRI _: false; Literal _: false esac;

isBlankObjectUnique state triple =
    empty?
       (filter (!= triple)
           (state.store.match 
                { s = Wildcard (), p = Wildcard (), o = Known triple.o }));

isBlankObjectUnwritten state triple =
    empty?
       (filter do t: state.written[t] done
           (state.store.match
                { s = Known triple.o, p = Wildcard (), o = Wildcard () }));

writeTurtleTripleMaybe state triple =
    if not state.written[triple] then
        isAnon =
            hasBlankObject triple and
            isBlankObjectUnique state triple and
            isBlankObjectUnwritten state triple;
        isCollection = 
            isAnon and
            isCollectionNode state.store triple.o;
        state.written[triple] := true;
        writeTripleParts (state with { isAnon, isCollection }) triple;
    else
        state
    fi;

writePrefixes handle store =
    for (store.prefixes ()) do pfx:
        case store.expand (pfx ^ ":") of
        IRI iri: 
            handle.write "@prefix \(pfx): <\(iri)> .\n"
        esac
    done;

writeTurtleTriples state triples =
   \() (fold writeTurtleTripleMaybe state triples);

writeTurtleTriples' := writeTurtleTriples;

sortByIri = sortBy
    do t1 t2:
        case t1.s of
        IRI i1:
            case t2.s of
            IRI i2: i1 < i2;
             _: true;
            esac;
         _: false;
        esac;
    done;

writeToNTriplesHandle handle store =
    for (store.enumerate ()) do t:
        writeNTriple handle t;
        handle.write ".\n";
    done;

//!!! need base
writeToNTriplesFile filename store =
    writeToNTriplesHandle (openOutFile filename "US-ASCII") store;

writeToTurtleHandle handle store =
   (writePrefixes handle store;
    handle.write "\n";
    tt = store.enumerate ();
    written = mapIntoHash id \(false) tt;
    ordered = // iri-subject triples first, then blank-subject triples
        sortByIri (filter do t: not hasBlankSubject t done tt)
        ++
        filter hasBlankSubject tt;
    writeTurtleTriples {
        handle,
        subject = None (),
        predicate = None (),
        indent = 0,
        isAnon = false,
        isCollection = false,
        store,
        written
    } ordered;
    if not (empty? ordered) then
        handle.write ".";
    fi;
    handle.write "\n";
    handle.close ());

//!!! need base
writeToTurtleFile filename store =
    writeToTurtleHandle (openOutFile filename "UTF-8") store;

{
  writeToNTriplesHandle,
  writeToNTriplesFile,
  writeToTurtleHandle,
  writeToTurtleFile
}
