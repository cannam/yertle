
module yertle.write;

store = load yertle.store;

writeNode handle node =
    case node of
    IRI u:
        handle.write "<\(u)>";
    Blank b:
        handle.write "_:blank\(b)";
    Literal l:
       (handle.write '"';
        handle.write l.value;
        handle.write '"';
        if l.language != "" then
            handle.write "@\(l.language)";
        elif l.type != "" then
            handle.write "^^";
            writeNode handle (IRI l.type);
        fi)
    esac;

writeNTriplesToHandle handle st =
    for (store.enumerate st) do t:
        for [ (.s), (.p), (.o) ] do sel:
            writeNode handle (sel t);
            handle.write " ";
        done;
        handle.write ".\n";
    done;

{ writeNTriplesToHandle }
