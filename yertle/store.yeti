/*
    Yertle
    Simple RDF store

    Copyright (c) 2013 Chris Cannam
    Copyright (c) 2013 Queen Mary, University of London
  
    Permission is hereby granted, free of charge, to any person
    obtaining a copy of this software and associated documentation
    files (the "Software"), to deal in the Software without
    restriction, including without limitation the rights to use, copy,
    modify, merge, publish, distribute, sublicense, and/or sell copies
    of the Software, and to permit persons to whom the Software is
    furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be
    included in all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
    EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
    MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
    NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR
    ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
    CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
    WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

    Except as contained in this notice, the names of the Centre for
    Digital Music; Queen Mary, University of London; and Chris Cannam
    shall not be used in advertising or otherwise to promote the sale,
    use or other dealings in this Software without prior written
    authorization.
*/

module yertle.store;

index = load yertle.index;

load yertle.prefix;

typedef iri = IRI string;

typedef fullnode =
    IRI. string | Blank. number |
        Literal. { 
            .value is string,
            .type is string,
            .language is string
        };

typedef triple = {
    .s is IRI. string | Blank. number,
    .p is IRI. string,
    .o is fullnode,
};

typedef tripleselector = triple -> fullnode;

typedef fullpatnode = Wildcard. () | Known. fullnode;

typedef pattern = {
    .s is Wildcard. () | Known. (IRI. string | Blank. number),
    .p is Wildcard. () | Known. (IRI. string),
    .o is fullpatnode,
};

typedef patternselector = pattern -> fullpatnode;

typedef indextree = hash<fullnode,
                         Sub hash<fullnode,
                                  Sub hash<fullnode,
                                           Entry array<fullnode>>>>;

typedef index = {
    .order is list<tripleselector>,
    .patternOrder is list<patternselector>,
    .tree is indextree
};

typedef storetype = {
    .prefixes is prefixtable,
    .indexes is list<index>,
};

newStore () = 
   (desired = [ "spo", "pos", "ops" ];
    orderMap = [ "s": (.s), "p": (.p), "o": (.o) ];
    patternOrderMap = [ "s": (.s), "p": (.p), "o": (.o) ];
    prefixes = newPrefixTable ();
    addPrefix prefixes "rdf" "http://www.w3.org/1999/02/22-rdf-syntax-ns#";
    addPrefix prefixes "xsd" "http://www.w3.org/2001/XMLSchema#";
    {
        prefixes,
        indexes =
            map do name:
                index.newIndex
                    (map (at orderMap) (tail (strSplit "" name)))
                    (map (at patternOrderMap) (tail (strSplit "" name)))
                done
                desired
    }
    );

add st t =
    all id (map do ix: index.add ix t done st.indexes);

remove st t =
    all id (map do ix: index.remove ix t done st.indexes);

enumerate st =
    index.enumerate (head st.indexes);

contains st t =
    // Doesn't matter which index we use to look up a complete triple
    index.contains (head st.indexes) t;

match st pat =
    index.match (index.chooseIndex st.indexes pat) pat;

norec addPrefix st prefix full = addPrefix st.prefixes prefix full;

norec havePrefix st prefix = havePrefix st.prefixes prefix;

norec prefixes st = prefixes st.prefixes;

expand' st pname = 
    if pname == "a" then 
        expand' st "rdf:type"
    else
        IRI (expand st.prefixes pname);
    fi;

typedef opaque store = storetype;

{ 
newStore, 
add, 
remove, 
enumerate, 
contains, 
match, 
addPrefix, 
havePrefix,
prefixes, 
expand = expand'
} as {
newStore is () -> store,
add is store -> triple -> boolean,
remove is store -> triple -> boolean,
enumerate is store -> list<triple>,
contains is store -> triple -> boolean,
match is store -> pattern -> list<triple>,
addPrefix is store -> string -> string -> (),
havePrefix is store -> string -> boolean,
prefixes is store -> list<string>,
expand is store -> string -> iri,
}


