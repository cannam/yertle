/*
    Yertle
    RDF/Turtle parser spec tests

    Copyright (c) 2013 Chris Cannam
    Copyright (c) 2013 Queen Mary, University of London
  
    Permission is hereby granted, free of charge, to any person
    obtaining a copy of this software and associated documentation
    files (the "Software"), to deal in the Software without
    restriction, including without limitation the rights to use, copy,
    modify, merge, publish, distribute, sublicense, and/or sell copies
    of the Software, and to permit persons to whom the Software is
    furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be
    included in all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
    EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
    MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
    NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR
    ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
    CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
    WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

    Except as contained in this notice, the names of the Centre for
    Digital Music; Queen Mary, University of London; and Chris Cannam
    shall not be used in advertising or otherwise to promote the sale,
    use or other dealings in this Software without prior written
    authorization.
*/

module test.test_spec;

{ compare, compareUnordered } = load test.test;

{ newStore, enumerate, contains, match, expand } = load yertle.store;
{ loadTurtleFile, parseTurtleFile } = load yertle.read;
{ writeNTriplesToHandle } = load yertle.write;

testFileDir = "test/ttl/spec";
outFileDir = "test/out";

manifest = loadTurtleFile "" (testFileDir ^ "/manifest.ttl");

tests = [:];

baseIri = "http://example/base/";

testPath filename = testFileDir ^ "/" ^ filename;

firstResultObjectText results =
    case results of
    [r]:
        case r.o of 
        Literal lit: lit.value;
        IRI iri: iri;
        _: "";
        esac;
     _: "";
    esac;

metadata mstore iri =
   (comments = match mstore { s = Known (iri),
//!!! ideally should be able to say Known (Abbr "rdfs:comment") or similar
                              p = Known (expand mstore "rdfs:comment"),
                              o = Wildcard () };
    actions  = match mstore { s = Known (iri),
                              p = Known (expand mstore "mf:action"),
                              o = Wildcard () };
    results  = match mstore { s = Known (iri),
                              p = Known (expand mstore "mf:result"),
                              o = Wildcard () };
    {
        name = firstResultObjectText comments,
        action = firstResultObjectText actions,
        result = firstResultObjectText results,
    });

checkParse isPositive filename =
   (err =
        find \case of Error e: true; _: false esac
           (parseTurtleFile (baseIri ^ filename) (testPath filename));
    if isPositive then
        if empty? err then true
        else println "\n** In parsing \(filename):\n   \(head err)"; false fi
    else
        if not empty? err then true
        else println "\n** Erroneously accepted \(filename) in negative test"; false fi
    fi);

compareNTriples a b =
   (ah = openInFile a "US-ASCII"; // Unlike Turtle, NTriples is ASCII
    bh = openInFile b "US-ASCII";
    rewrite = map (substAll "_:[a-zA-Z]+[a-zA-Z0-9]*\\b" "{blanknode}");
    atext = sort (rewrite (ah.lines ()));
    btext = sort (rewrite (bh.lines ()));
    ah.close ();
    bh.close ();
    if atext == btext then true
    else
        println "\n** Error: \"\(a)\" differs from expected \"\(b)\"";
        println "   First differing line (expected result first):";
        diff = head (find do p: p.fst != p.snd done (map2 pair btext atext));
        println "1. \(diff.fst)";
        println "2. \(diff.snd)";
        false;
    fi);

addTest mstore type p =
   (iri = p.s;
    meta = metadata mstore iri;
    if meta.name == "" or meta.action == "" then
        println "Error: Unable to retrieve test metadata for test: \(iri)";
    else
        func = case type of
        Positive (): checkParse true;
        Negative (): checkParse false;
        Evaluation ():
            do filename:
                checkParse true filename and
                   (fstore =
                        case loadTurtleFile (baseIri ^ filename) (testPath filename) of
                        Store s: s;
                        Error e: println "Error: \(e)"; newStore ();
                        esac;
                    outfile = outFileDir ^ "/" ^ meta.result;
                    handle = openOutFile outfile "UTF-8";
                    writeNTriplesToHandle handle fstore;
                    handle.close ();
                    compareNTriples outfile (testPath meta.result))
            done;
        esac;
        tests[meta.name] :=
          \(try func meta.action;
            catch Exception e: e#printStackTrace(); false;
            yrt);
    fi);

case manifest of
    Error e:
        println "Error: \(e)";
        tests["manifest load failed"] := \(false);
    Store mstore:
       (if length (enumerate mstore) < 10 then
            tests["too few tests in manifest: expected 10 or more"] := \(false);
        fi;
        for (match mstore { s = Wildcard (),
                            p = Known (expand mstore "a"),
                            o = Known (expand mstore "rdft:TestTurtlePositiveSyntax")
                         })
            (addTest mstore (Positive ()));
        for (match mstore { s = Wildcard (),
                            p = Known (expand mstore "a"),
                            o = Known (expand mstore "rdft:TestTurtleNegativeSyntax")
                         })
            (addTest mstore (Negative ()));
        for (match mstore { s = Wildcard (),
                            p = Known (expand mstore "a"),
                            o = Known (expand mstore "rdft:TestTurtleEval")
                         })
            (addTest mstore (Evaluation ()));
        for (match mstore { s = Wildcard (),
                            p = Known (expand mstore "a"),
                            o = Known (expand mstore "rdft:TestTurtleNegativeEval")
                         })
            (addTest mstore (Negative ()));
        );
esac;

tests is hash<string, () -> boolean>;

