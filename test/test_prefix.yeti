/*
    Yertle
    RDF/Turtle parser module

    Copyright (c) 2013-2014 Chris Cannam
    Copyright (c) 2013-2014 Queen Mary, University of London
  
    Permission is hereby granted, free of charge, to any person
    obtaining a copy of this software and associated documentation
    files (the "Software"), to deal in the Software without
    restriction, including without limitation the rights to use, copy,
    modify, merge, publish, distribute, sublicense, and/or sell copies
    of the Software, and to permit persons to whom the Software is
    furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be
    included in all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
    EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
    MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
    NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR
    ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
    CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
    WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

    Except as contained in this notice, the names of the Centre for
    Digital Music; Queen Mary, University of London; and Chris Cannam
    shall not be used in advertising or otherwise to promote the sale,
    use or other dealings in this Software without prior written
    authorization.
*/

module test.test_prefix;

{ compare, compareUnordered } = load test.test;

{ newPrefixTable, addPrefix, expand, abbreviate, prefixes } = load yertle.prefix;

makeTestTable () =
   (m = newPrefixTable ();
    addPrefix m "fruit" "http://example.com/fruit/";
    addPrefix m "fruitloop" "http://example.com/fruit/loop/";
    addPrefix m "veg" "http://example.com/vegetable/";
    addPrefix m "" "empty";
    m);

[

"empty": \(
    compareUnordered (prefixes (newPrefixTable ())) [];
),

"empty-expand": \(
    compare (expand (newPrefixTable ()) "a:b") "a:b";
),

"empty-abbreviate": \(
    compare (abbreviate (newPrefixTable ()) "a:b") "a:b";
),

"add": \(
    m = newPrefixTable ();
    addPrefix m "a" "b";
    compareUnordered (prefixes m) ["a"];
),

"add-another": \(
    m = newPrefixTable ();
    addPrefix m "a" "b";
    addPrefix m "aa" "bb";
    compareUnordered (prefixes m) ["a", "aa"];
),

"replace": \(
    m = newPrefixTable ();
    addPrefix m "a" "b";
    addPrefix m "a" "bb";
    compareUnordered (prefixes m) ["a"];
),

"expand": \(
    m = makeTestTable ();
    compare (expand m "veg:aubergine") "http://example.com/vegetable/aubergine"
    and compare (expand m "fruit:banana:thing") "http://example.com/fruit/banana:thing"
    and compare (expand m "fruitloop:banana") "http://example.com/fruit/loop/banana"
    and compare (expand m "fruit:loop/banana") "http://example.com/fruit/loop/banana"
    and compare (expand m "fruit:") "http://example.com/fruit/"
    and compare (expand m "fruit") "fruit"
    and compare (expand m ":fruit") "emptyfruit"
),

"abbreviate": \(
    m = newPrefixTable ();
    addPrefix m "1" "a";
    addPrefix m "2" "ab";
    addPrefix m "3" "aba";
    addPrefix m "4" "abba";
    addPrefix m "5" "abc";
    addPrefix m "6" "bbc";
    addPrefix m "7" "bbf";
    compare (abbreviate m "a") "1:"
    and compare (abbreviate m "aa") "1:a"
    and compare (abbreviate m "abc") "5:"
    and compare (abbreviate m "abd") "2:d"
    and compare (abbreviate m "bbb") "bbb"
    and compare (abbreviate m "bbd") "bbd"
    and compare (abbreviate m "abad") "3:d"
),    

"abbreviate-lifelike": \(
    m = makeTestTable ();
    compare (abbreviate m "http://example.com/vegetable/aubergine") "veg:aubergine"
    and compare (abbreviate m "http://example.com/fruit/banana:thing") "fruit:banana:thing"
    and compare (abbreviate m "http://example.com/fruit/loop/banana") "fruitloop:banana"
    and compare (abbreviate m "http://example.com/fruit/loop:banana") "fruit:loop:banana"
    and compare (abbreviate m "http://example.com/fruit/") "fruit:"
    and compare (abbreviate m "fruit") "fruit"
    and compare (abbreviate m "emptyfruit") ":fruit"
),

] is hash<string, () -> boolean>;

