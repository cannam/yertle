/*
    Yertle
    RDF/Turtle parser module

    Copyright (c) 2013-2014 Chris Cannam
    Copyright (c) 2013-2014 Queen Mary, University of London
  
    Permission is hereby granted, free of charge, to any person
    obtaining a copy of this software and associated documentation
    files (the "Software"), to deal in the Software without
    restriction, including without limitation the rights to use, copy,
    modify, merge, publish, distribute, sublicense, and/or sell copies
    of the Software, and to permit persons to whom the Software is
    furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be
    included in all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
    EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
    MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
    NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR
    ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
    CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
    WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

    Except as contained in this notice, the names of the Centre for
    Digital Music; Queen Mary, University of London; and Chris Cannam
    shall not be used in advertising or otherwise to promote the sale,
    use or other dealings in this Software without prior written
    authorization.
*/

module test.test_read;

{ compare, compareUnordered } = load test.test;

{ parseTurtleFile, parseTurtleString } = load yertle.read;

testFileDir = "test/ttl";

checkTriples res name =
   (err = find \case of Error e: true; _: false esac res;
    if empty? err then
        if empty? res or res == [End ()] then
            println "\n** No triples returned when parsing \(name)"; false
        else
            true
        fi
    else
        println "\n** In parsing \(name):\n   \(head err)"; false
    fi);

checkFailed res name =
   (err = find \case of Error e: true; _: false esac res;
    if empty? err then
        println "\n** Erroneously accepted \(name) in negative test (result was: \(res))"; false
    else
        true
    fi);

checkParse filename =
   (res = parseTurtleFile "" filename;
    checkTriples res filename);

tests = mapIntoHash id do n: \(checkParse "\(testFileDir)/\(n).ttl") done
[
    "bnode-nested-2", "bnode-nested", "bnode", "boolean", "collections",
    "example1", "example2", "example3", "goblin", "iris", "numbers", "quoted",
];

goodString str =
   (res = parseTurtleString "" str;
    checkTriples res "string \"\(str)\"");

badString str =
   (res = parseTurtleString "" str;
    checkFailed res "string \"\(str)\"");

tests["simple-string"]   := \(goodString "<a> <b> <c>.");
tests["simple-with-a"]   := \(goodString "<a> a <c> .");
tests["empty-prefixes"]  := \(goodString "@prefix : <>. :a :b :c.");

tests["simplestring"]    := \(badString ".");
tests["no-dot"]          := \(badString "<a> a <b>");
tests["ends-with-colon"] := \(badString "<a> a <b>;");
tests["lit-subject"]     := \(badString "\"a\" a <c>.");
tests["lit-predicate"]   := \(badString "<a> \"a\" <c>.");
tests["blank-predicate"] := \(badString "<a> _:a <c>.");
tests["local-bad"]       := \(badString ":a :b :c ." and
                              badString "@prefix : <>. :a:b :c ." and
                              badString "@prefix : <>. :a :b :.c ." and
                              badString "@prefix : <>. :a :b. :c ." and
                              badString "@prefix : <>. :a :b ::c");
tests["local-slash-bad"] := \(badString "@prefix : <>. :\\a :b :c ." and
                              badString "@prefix : <>. :\\: :b :c ." and
                              badString "@prefix : <>. :\\  :b :c ." and
                              badString "@prefix : <>. :\\\\  :b :c ." and
                              badString "@prefix : <>. :\\< :b :c .");

tests["local-colon"] := \(
    compare (parseTurtleString "" "@prefix : <>. :a: :b :c.")
        [ Prefix { fst = "", snd = "" },
          Triple { s = IRI "a:", p = IRI "b", o = IRI "c" } ];
);

tests["local-dot"] := \(
    compare (parseTurtleString "" "@prefix : <>. :a.b :b..c :c.d.")
        [ Prefix { fst = "", snd = "" },
          Triple { s = IRI "a.b", p = IRI "b..c", o = IRI "c.d" } ];
);

tests["prefix-dot"] := \(
    compare (parseTurtleString "" "@prefix a.b: <a>. a.b:a a.b:b.c a.b:c:d .")
        [ Prefix { fst = "a.b", snd = "a" },
          Triple { s = IRI "aa", p = IRI "ab.c", o = IRI "ac:d" } ];
);

tests["local-u-escape"] := \(
    compare (parseTurtleString "" "@prefix : <>.:\u0061bc :a\u0062c :ab\u0063.")
        [ Prefix { fst = "", snd = "" },
          Triple { s = IRI "abc", p = IRI "abc", o = IRI "abc" } ]
);

tests["local-pc-escape"] := \(
    compare (parseTurtleString "" "@prefix : <>.:%61bc :a%62c :ab%63.")
        [ Prefix { fst = "", snd = "" },
          Triple { s = IRI "abc", p = IRI "abc", o = IRI "abc" } ]
);

tests["local-slash-escape"] := \(
    compare (parseTurtleString "" "@prefix : <>.:\\& :a\\#c :ab\\(.")
        [ Prefix { fst = "", snd = "" },
          Triple { s = IRI "&", p = IRI "a#c", o = IRI "ab(" } ]
);

tests is hash<string, () -> boolean>;

